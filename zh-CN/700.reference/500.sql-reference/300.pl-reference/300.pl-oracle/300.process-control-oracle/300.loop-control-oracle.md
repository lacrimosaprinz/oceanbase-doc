| Description   |                 |
|---------------|-----------------|
| keywords      |                 |
| dir-name      |                 |
| dir-name-en   |                 |
| tenant-type   | Oracle Mode     |

# 循环控制 

循环语句主要用于反复执行一段操作。

  <main id="notice" >
    <h4>功能适用性</h4>
    <p>该内容仅适用于 OceanBase 数据库企业版。OceanBase 数据库社区版仅提供 MySQL 模式。</p>
  </main>

`LOOP` 声明包括以下四种形式：

* 基本 `LOOP`

  

* `FOR LOOP`

  

* 用于游标的 `FOR LOOP`

  

* `WHILE LOOP`

  




用于退出整个循环的声明包括以下两种形式：

* `EXIT`

  

* `EXIT WHEN`

  




用于退出本轮循环的声明包括以下两种形式：

* `CONTINUE`

  

* `CONTINUE WHEN`

  




`EXIT`、`EXIT WHEN`、`CONTINUE` 和 `CONTINUE WHEN` 用于退出循环，可以放置在循环中的任何位置。这是推荐使用的退出循环的方法，要避免使用 `GOTO` 语句退出循环的方式。`EXIT` 语句会将控制转移到 `LOOP` 结束处；`CONTINUE` 语句会退出循环的当前迭代，并将控制转移到下次迭代开始处；`EXIT` 和 `CONTINUE` 都可以跟一个可选的 `WHEN` 子句，其中可以指定一个提交。

如果是多层的嵌套循环，推荐给循环打上标签，这样会增加可读性。

基本 LOOP 
----------------------------

基本的 `LOOP` 语句会重复执行一序列语句。基本 `LOOP` 的语句结构如下：

```javascript
[ label ] LOOP 
    statements
END LOOP [ label ];
```



使用 `LOOP` 循环语句，`statements` 语句会循环执行。为了避免死循环，`LOOP` 和 `END LOOP` 之间的语句中，至少要有一个语句包含 `EXIT` 语句；否则 `LOOP` 语句会一直重复永不停止。

`EXIT` 语句可以带一个可选的 `WHEN` 子句，表示当条件为 `TRUE` 的时候，执行 `EXIT` 语句并将控制跳转到 `END LOOP` 语句后。

EXIT 语句 
----------------------------

`EXIT` 语句表示无条件退出当前循环。

示例如下：

```javascript
obclient> DECLARE
    cnt NUMBER := 0;
BEGIN
    LOOP
        DBMS_OUTPUT.PUT_LINE ('INSIDE: ' || TO_CHAR(cnt));
        cnt := cnt + 1;
        IF cnt > 5 THEN
         EXIT;
        END IF;
     END LOOP;
     DBMS_OUTPUT.PUT_LINE('OUTSIDE: ' || TO_CHAR(cnt));
END;
/
Query OK, 0 rows affected 

INSIDE: 0
INSIDE: 1
INSIDE: 2
INSIDE: 3
INSIDE: 4
INSIDE: 5
OUTSIDE: 6
```



EXIT WHEN 语句 
---------------------------------

当 `WHEN` 子句的条件为 `TRUE` 时，`EXIT WHEN` 语句用于退出当前循环。

示例如下：

```javascript
obclient> DECLARE
    cnt NUMBER := 0;
BEGIN
    LOOP
       DBMS_OUTPUT.PUT_LINE ('INSIDE: ' || TO_CHAR(cnt));
       cnt := cnt + 1;
       EXIT WHEN cnt >5;
     END LOOP;
     DBMS_OUTPUT.PUT_LINE('OUTSIDE: ' || TO_CHAR(cnt));
END;
/
Query OK, 0 rows affected 

INSIDE: 0
INSIDE: 1
INSIDE: 2
INSIDE: 3
INSIDE: 4
INSIDE: 5
OUTSIDE: 6
```



CONTINUE 语句 
--------------------------------

`CONTINUE` 语句用来退出本次循环，即略过从 `CONTINUE` 开始直到循环结束的代码，从循环头开始继续下一次。

```javascript
obclient> DECLARE
   cnt NUMBER := 0;
BEGIN
    LOOP
       cnt := cnt + 1;
       IF cnt < 4 THEN
         CONTINUE;
       END IF;
    DBMS_OUTPUT.PUT_LINE ('INSIDE: ' || TO_CHAR(cnt));
       EXIT WHEN cnt >5;
    END LOOP;
    DBMS_OUTPUT.PUT_LINE('OUTSIDE: ' || TO_CHAR(cnt));
END;
/
Query OK, 0 rows affected 

INSIDE: 4
INSIDE: 5
INSIDE: 6
OUTSIDE: 6
```



CONTINUE WHEN 语句 
-------------------------------------

`CONTINUE WHEN` 语句同样是用于退出本次循环，但只有在 `WHEN` 子句为 `TRUE` 的时候才会执行。 

示例如下：

```javascript
obclient>DECLARE
   cnt NUMBER := 0;
BEGIN
    LOOP
       cnt := cnt + 1;
       CONTINUE WHEN cnt < 4;
       DBMS_OUTPUT.PUT_LINE ('INSIDE: ' || TO_CHAR(cnt));
       EXIT WHEN cnt >5;
     END LOOP;
     DBMS_OUTPUT.PUT_LINE('OUTSIDE: ' || TO_CHAR(cnt));
END;
/
Query OK, 0 rows affected 

INSIDE: 4
INSIDE: 5
INSIDE: 6
OUTSIDE: 6
```



FOR LOOP 语句 
--------------------------------

`FOR LOOP` 会针对一个索引值范围，循环执行一段代码。语法如下：

```javascript
[ label ] FOR index IN [ REVERSE ] lower_bound..upper_bound LOOP 
    statements
END LOOP [ label ];
```



如下例所示，不加 `REVERSE` 子句时，`index` 会从 `lower_bound` 递增至 `upper_bound`；使用 `REVERSE` 子句时，`index` 会从 `upper_bound` 递减至 `lower_bound`。

```javascript
obclient> BEGIN
    FOR i IN 1..5 LOOP
        DBMS_OUTPUT.PUT_LINE (i);
    END LOOP;
END;
/
Query OK, 0 rows affected 

1
2
3
4
5

obclient>BEGIN
   FOR i IN REVERSE 1..5  LOOP
      DBMS_OUTPUT.PUT_LINE (i);
   END LOOP;
END;
/
Query OK, 0 rows affected 
5
4
3
2
1
```



`FOR LOOP` 语句的索引隐式声明为循环本地的 `PLS_INTEGER` 类型的变量。循环中的语句可以读取索引的值，但不能进行更改。循环外的语句无法引用索引。在 `FOR LOOP` 语句运行后，索引是未定义的状态。循环索引有时称为循环计数器。

`FOR LOOP` 语句的上下限可以是数字文本、数字变量或数字表达式。如果界限没有数字值，则 PL 会引发预定义异常 `VALUE_ERROR`。

WHILE LOOP 语句 
----------------------------------

`WHILE LOOP` 是另外一个循环控制的形式，会在判断条件结果为 `TRUE` 的时候，重复执行一序列语句。如果条件结果为 `FALSE` 了，则退出循环。语法结构如下：

```javascript
[ label ] WHILE condition LOOP 
    statements
END LOOP [ label ];
```



只要检测 `contidion` 值为 `TRUE`，循环就会继续，直至 `contidion` 值为 `FALSE` 或者 `NULL` 为止。

```javascript
obclient> DECLARE
    cnt NUMBER := 0;
BEGIN
    WHILE cnt < 3 LOOP
       DBMS_OUTPUT.PUT_LINE (cnt);
       cnt :=  cnt + 1;
    END LOOP;
END;
/
Query OK, 0 rows affected 

0
1
2
```


