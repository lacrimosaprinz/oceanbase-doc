| Description   |                 |
|---------------|-----------------|
| keywords      |                 |
| dir-name      |                 |
| dir-name-en   |                 |
| tenant-type   | Oracle Mode     |

# FIRST 和 LAST 方法 

如果集合具有至少一个元素，则 `FIRST` 和 `LAST` 分别返回第一个元素和最后一个元素的索引（忽略删除的元素，即使 `DELETE` 为其删除的元素保留了占位符）。

如果集合只有一个元素，则 `FIRST` 和 `LAST` 返回相同的索引。如果集合为空，则 `FIRST` 和 `LAST` 返回 `NULL`。

  <main id="notice" >
    <h4>功能适用性</h4>
    <p>该内容仅适用于 OceanBase 数据库企业版。OceanBase 数据库社区版仅提供 MySQL 模式。</p>
  </main>

在关联数组的用法 
-----------------

对于索引类型为 `PLS_INTEGER` 的关联数组，第一个和最后一个元素分别是具有最小和最大索引的元素。对于索引类型为字符串的关联数组，第一个和最后一个元素分别是具有最低和最高键值的元素。

示例如下：

```javascript
obclient> DECLARE
  TYPE numlist IS TABLE OF NUMBER INDEX BY VARCHAR2(5);
  nl NUMLIST;
  
  PROCEDURE print_first_and_last IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE('FIRST = ' || nl.FIRST);
    DBMS_OUTPUT.PUT_LINE('LAST = ' || nl.LAST);
  END print_first_and_last;
 
BEGIN
  nl('A') := 2;
  nl('C') := 4;
  nl('D') := 6;
  nl('E') := 8;
  nl('B') := 10;
  
  DBMS_OUTPUT.PUT_LINE('Before deleting:');
  print_first_and_last;
 
  nl.DELETE('A');
  nl.DELETE('E');
 
  DBMS_OUTPUT.PUT_LINE('After deleting:');
  print_first_and_last;
END;
/
Query OK, 0 rows affected 

Before deleting:
FIRST = A
LAST = E
After deleting:
FIRST = B
LAST = D
```



上述示例显示了索引类型为字符串的关联数组的 `FIRST` 和 `LAST` 的值，然后删除了第一个和最后一个元素，再次显示了 `FIRST` 和 `LAST` 的值。

在可变数组的用法 
-----------------

对于不为空的可变数组，`FIRST` 始终返回 `1`。对于任何可变数组，`LAST` 始终等于 `COUNT`。

示例如下：

```javascript
obclient> DECLARE
  TYPE players IS VARRAY(5) OF VARCHAR2(20);   
  name PLAYERS;
  
  PROCEDURE print_name (heading VARCHAR2) IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE(heading);
    
    IF name IS NULL THEN
      DBMS_OUTPUT.PUT_LINE('Does not exist');
    ELSIF name.FIRST IS NULL THEN
      DBMS_OUTPUT.PUT_LINE('No members');
    ELSE
      FOR i IN name.FIRST..name.LAST LOOP
        DBMS_OUTPUT.PUT_LINE(i || '. ' || name(i));
      END LOOP;
    END IF;
  
    DBMS_OUTPUT.PUT_LINE('---'); 
  END;
  
BEGIN 
  print_name('Team Members:');
  
  name := PLAYERS();  -- 团队没有成员
  print_name('Team Members:');
  
  name := players('Andrew', 'Barton');
  print_name('Team Members:');
  name := players('Charles', 'Carl', 'James', 'Gary','Brian');
  print_name('New Team Members:');
END;
/
Query OK, 0 rows affected 

Team Members:
Does not exist
---
Team Members:
No members
---
Team Members:
1. Andrew
2. Barton
---
New Team Members:
1. Charles
2. Carl
3. James
4. Gary
5. Brian
---
```



在嵌套表的用法 
----------------

对于嵌套表，`LAST` 等于 `COUNT`，除了从中间删除元素的情况，会使 `LAST` 大于 `COUNT`。

示例如下：

```javascript
obclient> DECLARE
  TYPE players IS TABLE OF VARCHAR2(20);   
  name PLAYERS;
  
  PROCEDURE print_name (heading VARCHAR2) IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE(heading);
    
    IF name IS NULL THEN
      DBMS_OUTPUT.PUT_LINE('Does not exist');
    ELSIF name.FIRST IS NULL THEN
      DBMS_OUTPUT.PUT_LINE('No members');
    ELSE
      FOR i IN name.FIRST..name.LAST LOOP
        DBMS_OUTPUT.PUT_LINE(i || '.');
        IF name.EXISTS(i) THEN
          DBMS_OUTPUT.PUT_LINE(name(i));
        ELSE
          DBMS_OUTPUT.PUT_LINE('TBD');
        END IF;
      END LOOP;
    END IF;
  
    DBMS_OUTPUT.PUT_LINE('---'); 
  END;
  
BEGIN 
  print_name('Team Members:');
  
  name := players();  -- 存在足球队，但没有球员
  print_name('Team Members:');
  
  name := players('Charles', 'Carl', 'James', 'Gary','Brian');  --加入球员
  print_name('Team Members:');
  
  name.DELETE(3,4);  -- 删除第 3 和第 4 名球员
  print_name('Current Team Members:');
END;
/
Query OK, 0 rows affected 

Team Members:
Does not exist
---
Team Members:
No members
---
Team Members:
1.
Charles
2.
Carl
3.
James
4.
Gary
5.
Brian
---
Current Team Members:
1.
Charles
2.
Carl
3.
TBD
4.
TBD
5.
Brian
---
```



上述示例使用带有 `team.FIRST` 和 `team.LAST` 的 `FOR LOOP` 语句输出嵌套表 `name`。因为嵌套表可以是稀疏的，所以仅当 `team.EXISTS(i)` 为 `TRUE` 时，`FOR LOOP` 语句才会输出 `team(i)`。
